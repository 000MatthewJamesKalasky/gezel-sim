ipblock arm1 {
  iptype "armsystem";
  ipparm "exec = fsldrive";
}

ipblock fsl1(in read     : ns(1);
             out data    : ns(32);
             out control : ns(1);
             out exists  : ns(1)) {
  iptype "xilinx_fsl_slave";
  ipparm "core=arm1";
  ipparm "slavewrite=0x80000000";
}

ipblock fsl2(in  write   : ns(1);
             in  data    : ns(32);
             in  control : ns(1);
             out full    : ns(1)) {
  iptype "xilinx_fsl_master";
  ipparm "core=arm1";
  ipparm "masterread=0x80000004";
  ipparm "masterstatus=0x80000008";
}

dp gezelfslcopy(  in  rdata   : ns(32);
                  in  exists  : ns(1);
                  out read    : ns(1);
                  out wdata   : ns(32);
                  in  full    : ns(1);
                  out write   : ns(1)) {
  reg rexists, rfull : ns(1);
  reg rcopy : ns(32);
  reg cnt   : ns(8);
  always {
    rexists  = exists;
    rfull    = full;
    wdata    = rcopy;
  }
  sfg dowrite   { write = 1; }
  sfg dontwrite { write = 0; }
  sfg doread    { read  = 1; }
  sfg dontread  { read  = 0; }
  sfg capture   { rcopy = rdata + 25; 
                  $display("captures data: ", $dec, rdata);
                }
}
fsm fsm_gezelfslcopy(gezelfslcopy) {
  initial s0;
  state s0w, s1;
  @s0  if (rexists) then (capture , doread, dontwrite) -> s1;
                    else (dontread, dontwrite) -> s0;
  @s1  if (rfull)   then (dontread, dontwrite) -> s1;
                    else (dontread, dowrite)   -> s0;
}

dp top {
  sig rdata, wdata       : ns(32);
  sig rcontrol, wcontrol : ns(1);
  sig write, read        : ns(1);
  sig exists, full       : ns(1);
  use arm1;
  use fsl1(read,  rdata, rcontrol, exists);
  use fsl2(write, wdata, wcontrol, full  );
  use gezelfslcopy(rdata, exists, read, 
                   wdata, full, write);
  always { wcontrol = 0; }
}

system S {
  top;
}
